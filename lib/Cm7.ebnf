cm7           ::= EOL* configs EOL song

configs       ::= key_config
key_config    ::= "key" WS? "=" WS? note
note          ::= [ACDFG] "#" | [ABDEG] "b" | [A-G]

song          ::= section? (EOL+ section?)*

section       ::= line*

line          ::= chord_line EOL lyrics_line EOL?

chord_line    ::= WS* chord (WS+ chord)* WS*
chord         ::= relative_note quality? addition* base?
quality       ::= "mM7" | "m7b5" | "m7" | "m" | "sus2" | "sus4" |"7" | "M7" | "6" | "9" | "aug7" | "aug" | "dim7" | "dim"
addition      ::= "add" modifier? interval
base          ::= "/" relative_note
relative_note ::= "1" | "2" | "3" | "4" | "5" | "6" | "7"
interval      ::= "2" | "4" | "6" | "9" | "11" | "13"
modifier      ::= "#" | "b"

lyrics_line   ::= WS* lyrics? (lyrics_beat+ lyrics?)* WS*
lyrics        ::= [^()\n\r]+
lyrics_beat   ::= "(" lyrics? ")"


WS            ::= [ \t]+
EOL           ::= [\n\r]

